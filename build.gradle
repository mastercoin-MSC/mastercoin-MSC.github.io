buildscript {
    repositories {
        jcenter()
        maven { url 'http://dl.bintray.com/aalmiray/asciidoctor' }
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:0.7.3'
        classpath 'org.ajoberstar:gradle-git:0.9.0'
    }
}

apply plugin: 'org.asciidoctor.gradle.asciidoctor'
apply plugin: 'github-pages'

def siteDir = "$buildDir/site"

asciidoctor {
    backend = 'html5'   // html5 only for now
//    backends = ['fopub','html5']  // Requires 0.8.0 asciidoctor plugin
    sourceDir = file("adoc")
    outputDir = file("$siteDir")
    baseDir = project.file('adoc')
    options = [
        attributes: [
            icons: 'font',
            'source-highlighter': 'prettify',
            experimental: true,
            copycss: true
        ]
    ]
}

githubPages {
  repoUri = 'https://github.com/mastercoin-MSC/mastercoin-MSC.github.io.git'
  targetBranch = 'master'

  pages {
     from(file("$siteDir")) {
     	into '.'
     }
  }
  credentials {
    username = 'msgilligan'
//    password = ''
  }
}


task copyStatic(type: Copy,
    group: 'render',
    description: "Copies static files to buildDir") {
    from "$rootDir"
    into "$siteDir"
    include "README.adoc"
}

task copyImages(type: Copy,
    group: 'render',
    description: "Copies static files to buildDir") {
    from "$rootDir/adoc/images"
    into "$siteDir/images"
//    include "README.adoc"
}


task generate(dependsOn: [copyStatic, copyImages, asciidoctor],
    group: 'render',
    description: "Generates static site in buildDir") {
    doLast {
        println "Static site generated in buildDir"
    }
}

publishGhPages.dependsOn generate

task publish(dependsOn: publishGhPages, group: 'render',
            description: "Publishes buildDir to Github Pages on 'master' branch")
